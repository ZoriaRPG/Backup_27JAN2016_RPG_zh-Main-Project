// An NPC variant that shows two messages with special styles.

ffc script Zelda
{
    void run(int message1, int message2)
    {
        bool talking=false;
        int slot1;
        int slot2;
        
        while(true)
        {
            if(!talking && !linkAlreadyTalking && LinkWantsToTalk(this))
            {
                // Get a string
                slot1=Tango_GetFreeSlot(TANGO_SLOT_NORMAL);
                if(slot1==TANGO_INVALID)
                    continue;
                
                // Reserve it so the next call to Tango_GetFreeString()
                // doesn't return the same one
                Tango_ReserveSlot(slot1);
                
                // Get another one
                slot2=Tango_GetFreeSlot(TANGO_SLOT_NORMAL);
                if(slot2==TANGO_INVALID)
                    continue;
                
                talking=true;
                linkAlreadyTalking=true;
                ShowMessages(slot1, slot2, message1, message2);
            }
            
            if(talking)
            {
                talking=Tango_SlotIsActive(slot1);
                if(!talking)
                    linkAlreadyTalking=false;
            }
            
            Waitframe();
        }
    }
    
    void ShowMessages(int slot1, int slot2, int message1, int message2)
    {
        Tango_ClearSlot(slot1);
        Tango_LoadMessage(slot1, message1);
        Tango_SetSlotStyle(slot1, STYLE_ZELDA);
        Tango_SetSlotPosition(slot1, 16, 8);
        Tango_ActivateSlot(slot1);
        
        Tango_ClearSlot(slot2);
        Tango_LoadMessage(slot2, message2);
        Tango_SetSlotStyle(slot2, STYLE_LINK);
        Tango_SetSlotPosition(slot2, 16, 90);
        Tango_ActivateSlot(slot2);
    }
}

